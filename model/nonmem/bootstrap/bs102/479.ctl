$PROBLEM ZERBAXA

$ABBR DERIV2=NO

$INPUT ID NUM TIME DV AMT RATE ADDL II CMT MDV EVID HEIGHT BW SEX AGE SOFA PCT ECMO CRRT ELWI CI CRE ALB PROTEIN

$DATA ../data/479.csv

$SUBROUTINE ADVAN5 TRANS1

$MODEL
COMP=(DEPOT1)  ;1-CEFTOLOZANE CENTR
COMP=(PER1)    ;2-CEFTOLOZANE PERIPH
COMP=(DEPOT2)  ;3-TAZOBACTAM CENTR
COMP=(PER2)    ;4-TAZOBACTAM PERIPH
$PK

   BSA =  0.20247*(HEIGHT/100)**0.725*BW**0.425 ;(DuBois & DuBois) 
   CLCR = (140-AGE)*BW/72/CRE*0.85**(SEX)       ;(Cockcroft-Gault formula)
   CO = CI*BSA;
   
   V1C = THETA(1)*EXP(ETA(1))       
   CLC = THETA(2)*EXP(ETA(2)) 
   V2C = THETA(3)*EXP(ETA(3))       
   QC  = THETA(4)*EXP(ETA(4)) 
   V1T = THETA(5)*EXP(ETA(5))  
   CLT = THETA(6)*EXP(ETA(6))  
   V2T = THETA(7)*EXP(ETA(7))       
   QT  = THETA(8)*EXP(ETA(8)) 
   
   
K10 = CLC/V1C
K12 = QC/V1C
K21 = QC/V2C
K30 = CLT/V1T
K34 = QT/V1T
K43 = QT/V2T



$ERROR
IF(CMT.EQ.1) THEN
IPRED=A(1)/V1C
Y=IPRED*(1+EPS(1))
ENDIF
IF(CMT.EQ.3) THEN
IPRED=A(3)/V1T
Y=IPRED*(1+EPS(2))
ENDIF

$THETA 
(0, 30.2)    ;THETA1 V1C  [L]
(0, 6.28)    ;THETA2 CLC  [L/h]   
(0, 55.4)    ;THETA3 V2C  [L]
(0, 5.9)     ;THETA4 QC   [L/h]   
(0, 50)      ;THETA5 V1T  [L]
(0, 16.8)    ;THETA6 CLT  [L/h]
(0, 3)       ;THETA7 V2T  [L]
(0, 20)      ;THETA8 QT   [L/h]

$OMEGA 
0 FIX         ;11 ETA-V1C
0.1           ;22 ETA-CLC
0 FIX         ;33 ETA-V2C
0 FIX         ;44 ETA-QC

$OMEGA 
0.2                ;55 ETA-V1T
0.2            ;66 ETA-CLT

$OMEGA 
0 FIX    ;77 ETA-V2T
0 FIX    ;88 ETA-QT

;--------SIGMAS
$SIGMA 0.2 ; 11 PROPC, %
$SIGMA 0.2 ; 22 PROPT, %

$EST METHOD=1 INTER NOABORT MAXEVAL=9990 NSIG=3 SIGL=9 PRINT=2 